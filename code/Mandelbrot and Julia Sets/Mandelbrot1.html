<!DOCTYPE html>
<html>
<head>
<meta http-equiv="Content-Type" content="text/html;charset=utf-8" > 

<title>Mandelbrot and Julia Sets</title>

<script id="vertex-shader" type="x-shader/x-vertex">
precision highp float; // use high precision

attribute vec4 vPosition;
varying vec2 position;

void main(){
    position = vPosition.xy;
	gl_Position = vPosition;
}
</script>

<script id="fragment-shader" type="x-shader/x-fragment">
precision highp float; // use high precision

varying vec2 position;

const int N = 1000;

uniform vec2 origin; 
uniform float s; // scale
uniform float p; // color exponent

vec3 hsv2rgb(vec3 c) {
  vec4 K = vec4(1.0, 2.0 / 3.0, 1.0 / 3.0, 3.0);
  vec3 p = abs(fract(c.xxx + K.xyz) * 6.0 - K.www);
  return c.z * mix(K.xxx, clamp(p - K.xxx, 0.0, 1.0), c.y);
}

vec4 mapToColor(float h){
	float s = 1.0;
	float v = 1.0;
	return vec4(hsv2rgb(vec3(h,s,v)), 1.0);
}


float nIterations(vec2 z, vec2 c){ /* normalized iterations */
	float x,y,a,b,rr,t;
	float RR = 400.0; // escape radius squared
	int n;
	
	x = z.x; y = z.y; 
	a = c.x; b = c.y;

    for(int i = 0; i< N; i++){
		rr = x*x + y*y;
		// map: (x+iy) -> (x+iy)^2 + (a+ib)
        t = x*x - y*y + a;
        y = 2.0*x*y + b;
        x = t;
		
		if(rr > RR){ n=i; break;}
		// Note that before we break, we do
		// one more iteration.
	}
	// One more iteration. 
	t = x*x - y*y + a;
    y = 2.0*x*y + b;
    x = t;
	/* The two additional iterations decrease the error. See this page
	   for the theory https://linas.org/art-gallery/escape/escape.html */
	rr = x*x + y*y;
	
	return float(n+2)-log2(0.5*log(rr));
}


void main(){
    vec2 pos = s*position + origin;
    vec2 z, c;

    //z = vec2(0.0, 0.0); c = pos;         // Mandelbrot Set

    // The rest are Julia Sets
    //z = pos; c = vec2(0.37, -0.283);
    //z = pos; c = vec2(0.18, -0.57);     
    //z = pos; c = vec2(-0.8, -0.175);
    //z = pos; c = vec2(0.2, -0.55);
    z = pos; c = vec2(0.107, 0.62); 


	float i = nIterations(z,c);
	float t =  pow(i/float(N), p);
	gl_FragColor = mapToColor(t); 
}


</script>

<script type="text/javascript" src="../Common/webgl-utils.js"></script>
<script type="text/javascript" src="../Common/initShaders.js"></script>
<script type="text/javascript" src="../Common/MV.js"></script>
<script type="text/javascript" src="Mandelbrot1.js"></script>
</head>

<body>
<canvas id="gl-canvas" width="1024" height="1024">
  HTML5 Canvas not supported!
</canvas>
<br/>
<input id="slider" type="range" 
     min="0", max="2", step="0.01" > Color Exponent 
</input>

</body>
</html>


<!DOCTYPE html>
<html>
<head>
<meta http-equiv="Content-Type" content="text/html;charset=utf-8" > 

<title>Noise</title>

<script id="vertex-shader" type="x-shader/x-vertex">
precision highp float; // use high precision
uniform float t; // time, for time varying functions

attribute float vx;

float PI = 3.14159;

#define SALT0 4.112
#define SALT1 9.725

float random(float x, float salt){
	// returns a value in (-1,1)
	float a = 9178.123; 
	float b = 311.456-salt;
	float c = 0.971+salt;
	return 2.0*fract(a*sin(b*x)+c)-1.0;
}

float lerp(float a, float b, float t){ // linear interpolation
	return (1.0-t)*a + t*b;
}

float cerp(float a, float b, float t){ // cosine interpolation
	float s = (1.0-cos(PI*t))/2.0;
	return (1.0-s)*a + s*b;
}

float alpha(float a, float b, float t){ // similar to smoothstep
    float s = (3.0 - 2.0*t)*t*t; // 3t^2 - 2t^3 
    return (1.0-s)*a + s*b;
}

float valueNoise(float x){
	float a = random(floor(x), SALT0);
	float b = random(ceil(x), SALT0);
	float f = fract(x);
	return alpha(a, b, f);
}

float gradientNoise(float x){
	float i = floor(x);
	float j = ceil(x);
	float f = x - i;
	float a = random(i, SALT0);
	float b = random(j, SALT0);
	float da = random(i, SALT1);
	float db = random(j, SALT1);
	return cerp(a + da*f, b - db*(1.0-f), f);
}


#define OCTAVES 8

float fbmVN(float x){
	float a = 1.0;
	float s = 0.0;
	for(int i = 0; i < OCTAVES; ++i){
		s += valueNoise(a*x)/a;
		a *= 2.0;
	}
	return s;
}

float fbm(float x){
	float a = 1.0;
	float s = 0.0;
	for(int i = 0; i < OCTAVES; ++i){
		s += gradientNoise(a*x)/a;
		a *= 2.0;
	}
	return s;
}

float turbulence(float x){
    float a = 1.0;
	float s = 0.0;
	for(int i = 0; i < OCTAVES; ++i){
		s += abs(gradientNoise(a*x)/a);
		a *= 2.0;
	}
	return s;
}

float f(float x){
	//return random(x, SALT0);
	//return valueNoise(x);
	//return gradientNoise(x);
	//return fbmVN(x);
	return fbm(x);
	//return turbulence(x);
	
}

void main(){
	float sx = 2.0;
	float sy = 2.0;
	float x = 2.0*vx - 1.0;
	gl_Position = vec4(x, f(sx*x)/sy ,0.0, 1.0);
}
</script>

<script id="fragment-shader" type="x-shader/x-fragment">
precision highp float; // use high precision

void main(){ 
	gl_FragColor = vec4(0,1,0,1); 
}
</script>

<script type="text/javascript" src="../Common/webgl-utils.js"></script>
<script type="text/javascript" src="../Common/initShaders.js"></script>
<script type="text/javascript" src="../Common/MV.js"></script>
<script type="text/javascript" src="Noise1d.js"></script>
</head>

<body style="background-color:grey;">
<canvas id="gl-canvas" width="512" height="512">
  HTML5 Canvas not supported!
</canvas>
</body>
</html>

